#compdef jsh
# shellcheck shell=bash
# shellcheck disable=SC2207,SC2296,SC2034,SC2154,SC2206,SC2155,SC2128,SC1087
# Zsh completion for jsh - dynamically generated from @jsh-* metadata

_jsh() {
    local curcontext="$curcontext" state line
    typeset -A opt_args

    local jsh_completion="${JSH_DIR:-${HOME}/.jsh}/bin/jsh-completion"

    _arguments -C \
        '1: :->command' \
        '*::arg:->args'

    case $state in
        command)
            local -a commands
            if [[ -x "$jsh_completion" ]]; then
                commands=(${(f)"$("$jsh_completion" commands 2>/dev/null)"})
            fi
            _describe -t commands 'jsh commands' commands
            ;;
        args)
            local cmd="${words[2]}"
            local prev_word="${words[CURRENT-1]}"
            local cur_word="${words[CURRENT]}"
            local -a subcommands

            # setup supports file-path arguments for --adopt / --decom.
            if [[ "${cmd}" == "setup" ]]; then
                if [[ "${prev_word}" == "--adopt" || "${prev_word}" == "--decom" ]]; then
                    _files
                    return
                fi
                if [[ "${cur_word}" == -* ]]; then
                    local -a options
                    if [[ -x "$jsh_completion" ]]; then
                        options=(${(f)"$("$jsh_completion" options "$cmd" 2>/dev/null)"})
                        _describe -t options "${cmd} options" options
                        return
                    fi
                fi
            fi

            # Get subcommands for this command (options available via --help)
            if [[ -x "$jsh_completion" ]]; then
                subcommands=(${(f)"$("$jsh_completion" subcommands "$cmd" 2>/dev/null)"})
            fi

            # Complete subcommands
            if [[ ${#words[@]} -eq 2 ]] && [[ ${#subcommands[@]} -gt 0 ]]; then
                _describe -t subcommands "${cmd} subcommands" subcommands
            fi
            ;;
    esac
}

_jsh "$@"
